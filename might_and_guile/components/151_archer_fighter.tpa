
//ADD MARKSMAN_______________________________________________________________________
//
DEFINE_ACTION_FUNCTION archer_fighter_kit BEGIN

LAM d5_archer_ranger

ACTION_IF (d5_remove_archer_ranger = 0) BEGIN

 LAF check_kit_conflict END

 ADD_KIT ~D5ARCHF~

//WEAPON PROFICIENCIES_______________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5ARCHF       0 0 0 0 0 0 0 0~
~D5ARCHF       0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 5 5 5 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5ARCHF           9   9   0   0   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5ARCHF           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT__________________________________________________
//                 STR DEX CON INT WIS CHR
~D5ARCHF           15  15  0   0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5ARCHF           15  15  0   0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5ARCHF           1  1  1  1  1  1  1  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5ARCHF           0  1  1  1  1  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/fighter/d5archf.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_F_H K_F_G K_F_D K_F_HE K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x40000000 2~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG24 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @15002
SAY @15003
SAY @15004
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
 ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@15004)
		STR_VAR
			kit_name = D5ARCHF
			clswpbon = ~1 0 2~
	END
 END
//___________________________________________________________________________________

//ARCHER ABILITY FILES_______________________________________________________________
//
 COPY ~might_and_guile/fighter/d5archf.2da~ ~override~

 COPY ~might_and_guile/misc/d5mthac.spl~ ~override~
//___________________________________________________________________________________

//WILD TALENT________________________________________________________________________
//
 ACTION_IF (FILE_EXISTS_IN_GAME ~d5__psionics.d5~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~d5archf.2da~) BEGIN
	APPEND ~d5archf.2da~ ~WILD_TLNT   AP_D5PSWT_  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~
  END
 END
//___________________________________________________________________________________


END	//	end  keep ranger version


//REMOVE RANGER VERSION?_____________________________________________________________
//
ACTION_IF (d5_remove_archer_ranger = 1) BEGIN

//add to fighter menus_______________________________________________________________
// 
  ACTION_FOR_EACH race IN ~D~ ~E~ ~G~ ~H~ ~HE~ ~HL~ ~HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~K_F_%race%.2da~ BEGIN
	  COPY_EXISTING ~K_F_%race%.2da~ ~override~
		COUNT_2DA_COLS cols
		COUNT_2DA_ROWS cols rows
		PATCH_IF (rows > 3) BEGIN
		  INSERT_2DA_ROW 4 cols ~4    7~
		END
	  BUT_ONLY
	END
  END

//remove from ranger menus___________________________________________________________
// 
  LAF remove_menu_kit INT_VAR del_kit = 7 STR_VAR class = ~R~ END

//change class in kitlist____________________________________________________________
// 
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS 9 rows
    FOR (row = 1; row < rows; ++row ) BEGIN
      READ_2DA_ENTRY row 1 9 kitname
      READ_2DA_ENTRY row 8 9 kitclass
      PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
        SET_2DA_ENTRY row 8 9 2
      END
    END
  BUT_ONLY

//remove some ranger abilities_______________________________________________________
// 
// ...don't actually need to!

//better with melee__________________________________________________________________
// 
  COPY_EXISTING ~weapprof.2da~ ~override~
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS 5 rows
    READ_2DA_ENTRIES_NOW r2en_arch (cols - 1)
    FOR (col = 3; col < (cols - 2); col += 1) BEGIN
      READ_2DA_ENTRY_FORMER r2en_arch 0 col kitname
      PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
        FOR (row = 8; row < 40; ++row) BEGIN
          READ_2DA_ENTRY_FORMER r2en_arch row (col + 1) val
          PATCH_IF (val = 1) BEGIN
            SET_2DA_ENTRY_LATER ~s2el_arch~ row (col + 1) 2
          END
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_arch~ 5
    SET_2DA_ENTRY 0 0 5 ~                      ID~
  BUT_ONLY

END	//	end remove ranger kit

//___________________________________________________________________________________


END	//	end function

