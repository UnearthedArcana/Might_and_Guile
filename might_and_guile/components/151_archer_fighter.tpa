
//________________________FIGHTER_ARCHER____________________________________________

DEFINE_ACTION_FUNCTION archer_fighter_kit BEGIN

//remove from ranger menus_________________________________________________________
// 
LAF remove_menu_kit INT_VAR del_kit = 7 STR_VAR class = ~R~ END

//add to fighter menus______________________________________________________________
// 
ACTION_FOR_EACH race IN ~D~ ~E~ ~G~ ~H~ ~HE~ ~HL~ ~HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~K_F_%race%.2da~ BEGIN
	  COPY_EXISTING ~K_F_%race%.2da~ ~override~
		COUNT_2DA_COLS cols
		COUNT_2DA_ROWS cols rows
		PATCH_IF (rows > 3) BEGIN
		  INSERT_2DA_ROW 3 cols ~3    7~
		END
	  BUT_ONLY
	END
END

//change class in kitlist___________________________________________________________
// 
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 1 9 kitname
    READ_2DA_ENTRY row 8 9 kitclass
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
      SET_2DA_ENTRY row 8 9 2
    END
  END
BUT_ONLY

//remove some ranger abilities______________________________________________________
// 
COPY_EXISTING ~clabrn02.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL311~ ~****      ~
IF_EXISTS BUT_ONLY

//liberalize alignments_____________________________________________________________
// 
COPY_EXISTING ~alignmnt.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 0 cols kitname
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) OR (~%kitname%~ STRING_EQUAL_CASE ~ARCHER~) BEGIN
      SET archer_row = %row%
    END
  END
  PATCH_IF (VARIABLE_IS_SET %archer_row%) BEGIN
    FOR (col = 1; col < cols; ++col ) BEGIN
      SET_2DA_ENTRY archer_row col cols 1
    END
  END
BUT_ONLY

//allow archer dual-class___________________________________________________________
// 
COPY_EXISTING ~dualclas.2da~ ~override~
  COUNT_2DA_ROWS 7 rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 0 7 kitname
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) BEGIN
      SET archer_row = %row%
    END
  END
  PATCH_IF (VARIABLE_IS_SET %archer_row%) BEGIN
    SET_2DA_ENTRY archer_row 2 7 1
    SET_2DA_ENTRY archer_row 3 7 1
    SET_2DA_ENTRY archer_row 4 7 1
    SET_2DA_ENTRY archer_row 5 7 1
  END
BUT_ONLY

COPY_EXISTING ~abdcscrq.2da~ ~override~
              ~abdcdsrq.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 0 cols kitname
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~FERALAN~) OR (~%kitname%~ STRING_EQUAL_CASE ~ARCHER~) BEGIN
      SET archer_row = %row%
    END
  END
  PATCH_IF (VARIABLE_IS_SET %archer_row%) BEGIN
    SET_2DA_ENTRY archer_row 2 cols 0
    SET_2DA_ENTRY archer_row 5 cols 0
  END
BUT_ONLY
//__________________________________________________________________________________


END	//	end function

