
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							IMPROVED RANGERS
//__________________________________________________________________________________
//__________________________________________________________________________________


DEFINE_ACTION_FUNCTION improved_rangers BEGIN


// copy marker file__________________________________________________________________
//
COPY ~might_and_guile/lib/markers/d5_marker.d5~ ~override/d5__imp_rangers.d5~
//___________________________________________________________________________________


//CHANGE CLASS TEXT_________________________________________________________________
//
ACTION_FOR_EACH text_file IN ~clastext~ ~sodcltxt~ ~bgclatxt~ BEGIN
  COPY_EXISTING ~%text_file%.2da~ ~override~
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY row 0 cols kit_name
      READ_2DA_ENTRY row 1 cols kitlist_name
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~RANGER~) BEGIN
        PATCH_IF !(FILE_EXISTS_IN_GAME ~d5rfor.2da~) BEGIN
          SET_2DA_ENTRY row 4 cols RESOLVE_STR_REF (@2401)
        END
        PATCH_IF (FILE_EXISTS_IN_GAME ~d5rfor.2da~) BEGIN
          SET_2DA_ENTRY row 4 cols RESOLVE_STR_REF (@2402)
        END
      END
    END
  IF_EXISTS BUT_ONLY
END


// first, get all rangers____________________________________________________________
// 
OUTER_SPRINT $d5_rangers_array(~RANGER~)~CLABRN01~

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 2; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 1 9 kit_name
    READ_2DA_ENTRY row 5 9 kit_clab
    READ_2DA_ENTRY row 8 9 kit_class
    PATCH_IF (kit_class = 12) BEGIN
      SPRINT $d5_rangers_array(~%kit_name%~)~%kit_clab%~
    END
  END
BUT_ONLY


// let all rangers backstab__________________________________________________________
// 
COPY_EXISTING ~backstab.2da~ ~override~
  COUNT_2DA_COLS backstab_cols
  PATCH_IF (backstab_cols < 45) BEGIN
    SPRINT new_backstab_row ~RANGER        1             1             1             1             1             1             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2~
  END
  PATCH_IF (backstab_cols > 44) BEGIN
    SPRINT new_backstab_row ~RANGER        1             1             1             1             1             1             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2             2~
  END
BUT_ONLY

//APPEND ~backstab.2da~ ~%new_backstab_row%~

ACTION_PHP_EACH d5_rangers_array AS kit => clab BEGIN
  ACTION_IF !(~%kit%~ STRING_EQUAL_CASE ~STALKER~) BEGIN
	OUTER_INNER_PATCH_SAVE new_backstab_row ~%new_backstab_row%~ BEGIN
	  REPLACE_TEXTUALLY ~RANGER~ ~%kit%~
	END
	APPEND ~backstab.2da~ ~%new_backstab_row%~
	APPEND ~sneakatt.2da~ ~%new_backstab_row%~
	OUTER_INNER_PATCH_SAVE new_backstab_row ~%new_backstab_row%~ BEGIN
	  REPLACE_TEXTUALLY ~%kit%~ ~RANGER~
	END
  END
END

COPY_EXISTING ~sneakatt.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 2; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 0 cols name
    PATCH_IF (~%name%~ STRING_EQUAL_CASE ~STALKER~) BEGIN
      SET stalker_row = row
    END
  END
  PATCH_IF (VARIABLE_IS_SET %stalker_row%) BEGIN
    SET_2DA_ENTRY stalker_row 6 cols 2
    SET_2DA_ENTRY stalker_row 7 cols 2
    SET_2DA_ENTRY stalker_row 8 cols 2
    SET_2DA_ENTRY stalker_row 10 cols 3
    SET_2DA_ENTRY stalker_row 11 cols 3
    SET_2DA_ENTRY stalker_row 12 cols 3
    SET_2DA_ENTRY stalker_row 13 cols 3
    SET_2DA_ENTRY stalker_row 14 cols 3
    SET_2DA_ENTRY stalker_row 15 cols 3
    SET_2DA_ENTRY stalker_row 16 cols 3
    SET_2DA_ENTRY stalker_row 19 cols 4
    SET_2DA_ENTRY stalker_row 20 cols 4
    SET_2DA_ENTRY stalker_row 21 cols 4
    SET_2DA_ENTRY stalker_row 22 cols 4
    SET_2DA_ENTRY stalker_row 23 cols 4
    SET_2DA_ENTRY stalker_row 24 cols 4
  END
BUT_ONLY

// 'alertness' - increased visual range______________________________________________
// 
COPY ~might_and_guile/lib/d5_base.spl~ ~override/d5rnvis.spl~
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 262 target = 1 parameter1 = 2 parameter2 = 0 timing = 9 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5rnvis~ END

ACTION_PHP_EACH d5_rangers_array AS kit => clab BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%clab%.2da~) BEGIN
    APPEND ~%clab%.2da~ ~ALERT       AP_D5RNVIS  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****         ****        ****        ****        ****        ****        ****  ~
  END
END
//___________________________________________________________________________________


//more ranger alignments_____________________________________________________________
//
ACTION_PHP_EACH d5_rangers_array AS kit => clab BEGIN
  APPEND ~fallen.2da~ ~%kit%%TAB%0~
  COPY_EXISTING ~alignmnt.2da~ ~override~
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    FOR (row = 3; row < rows; ++row) BEGIN
      READ_2DA_ENTRY row 0 cols align_kit
      PATCH_IF (~%align_kit%~ STRING_EQUAL_CASE ~%kit%~) BEGIN
        FOR (col = 1; col < cols; col += 1) BEGIN
          SET_2DA_ENTRY row col cols 1
        END
      END
    END
  BUT_ONLY
END
//__________________________________________________________________________________


ACTION_IF (FILE_EXISTS_IN_GAME ~d5__rfeat.d5~) BEGIN

// just apply feats if already installed______________________________________________
// 
ACTION_PHP_EACH d5_rangers_array AS kit => clab BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%clab%.2da~) BEGIN
    APPEND ~%clab%.2da~ ~FEATS       AP_D5VWC5L  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****         ****        ****        ****        ****        ****        ****  ~
    ACTION_IF !(GAME_IS ~iwdee~) BEGIN
      APPEND ~%clab%.2da~ ~FEATS       AP_D5VRA9L  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****         ****        ****        ****        ****        ****        ****  ~
    END
  END
END
//___________________________________________________________________________________

END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5__rfeat.d5~) BEGIN

// give tracking at level 1__________________________________________________________
//
 ACTION_IF (FILE_EXISTS_IN_GAME ~spcl922.spl~) BEGIN

  ACTION_IF !(GAME_IS ~iwdee~) BEGIN

	COPY_EXISTING_REGEXP GLOB ~^lu.+\.2da$~ ~override~
	  LPF patch_remove_hla STR_VAR remove_ability = ~GA_SPCL922~ END
	BUT_ONLY

//	APPEND ~clabrn01.2da~ ~TRACKING    GA_SPCL922  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****         ****        ****        ****        ****        ****        ****  ~

    ACTION_PHP_EACH d5_rangers_array AS kit => clab BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%clab%.2da~) BEGIN
        APPEND ~%clab%.2da~ ~TRACKING    GA_SPCL922  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****         ****        ****        ****        ****        ****        ****  ~
      END
    END

  END
  
 END
//___________________________________________________________________________________

 ACTION_IF (GAME_IS ~bgee bg2ee eet iwdee~) BEGIN

// give poison/disease/nausea evasion________________________________________________
// 
 INCLUDE ~might_and_guile/lib/spell_evasion.tpa~

 ACTION_IF !(FILE_EXISTS_IN_GAME ~d5poisev.2da~) BEGIN
<<<<<<<< d5/d5poisev.2da
2DA V1.0 
RES
>>>>>>>> 
 COPY ~d5/d5poisev.2da~ ~override/d5poisev.2da~ 
 COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
	GET_OFFSET_ARRAY2 fx_array ab_off SPL_V10_HEAD_EFFECTS
	PHP_EACH fx_array AS int => fx_off BEGIN
	  READ_SHORT fx_off fx_type
	  PATCH_IF fx_type = 25 BEGIN 							// poison
		PATCH_IF !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~SPPR722~) && !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~SPWI714~) BEGIN
//		  PATCH_PRINT "%SOURCE_RES% is a poison spell"
		  INNER_ACTION BEGIN
			APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	SPL~ UNLESS ~%SOURCE_RES%~
		  END
		END
	  END
	  PATCH_IF fx_type = 12 BEGIN 							// damage (poison)
    	READ_SHORT fx_off + 0xa damage_type
		PATCH_IF damage_type = 32 BEGIN
		  PATCH_IF !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~SPPR722~) && !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~SPWI714~) BEGIN
//			PATCH_PRINT "%SOURCE_RES% is a poison spell"
			INNER_ACTION BEGIN
			  APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	SPL~ UNLESS ~%SOURCE_RES%~
			END
		  END
		END
	  END
	  PATCH_IF fx_type = 78 BEGIN 							// disease
//		  PATCH_PRINT "%SOURCE_RES% is a disease spell"
		  INNER_ACTION BEGIN
			APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	SPL~ UNLESS ~%SOURCE_RES%~
		  END
	  END
	END
  END
 BUT_ONLY
 COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
	GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
	PHP_EACH fx_array AS int => fx_off BEGIN
	  READ_SHORT fx_off fx_type
	  PATCH_IF fx_type = 25 BEGIN 							// poison
//		  PATCH_PRINT "%SOURCE_RES% is a poison item"
		  INNER_ACTION BEGIN
			APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	ITM~ UNLESS ~%SOURCE_RES%~
		  END
	  END
	  PATCH_IF fx_type = 12 BEGIN 							// damage (poison)
    	READ_SHORT fx_off + 0xa damage_type
		PATCH_IF damage_type = 32 BEGIN
//			PATCH_PRINT "%SOURCE_RES% is a poison item"
			INNER_ACTION BEGIN
			  APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	ITM~ UNLESS ~%SOURCE_RES%~
			END
		END
	  END
	  PATCH_IF fx_type = 78 BEGIN 							// disease
//		  PATCH_PRINT "%SOURCE_RES% is a disease item"
		  INNER_ACTION BEGIN
			APPEND ~d5poisev.2da~ ~%SOURCE_RES% 	ITM~ UNLESS ~%SOURCE_RES%~
		  END
	  END
	END
  END
 BUT_ONLY
END	// end if not already present

COPY_EXISTING ~d5poisev.2da~ ~override~ 
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
//  READ_2DA_ENTRIES_NOW ~r2en_ranres~ cols
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 cols resist_res
	PATCH_IF FILE_EXISTS_IN_GAME ~%poison_evade_res%.spl~ BEGIN
	  INNER_ACTION BEGIN
		LAF add_evade_spell INT_VAR evasion_class = 12 evasion_save = 4 STR_VAR evade_ext = ~spl~ evade_condition = ~class~ evade_res = EVAL ~%resist_res%~ evade_prefix = ~D5RR~ END
	  END
	END
	PATCH_IF FILE_EXISTS_IN_GAME ~%poison_evade_res%.itm~ BEGIN
	  INNER_ACTION BEGIN
		LAF add_evade_spell INT_VAR evasion_class = 12 evasion_save = 4 STR_VAR evade_ext = ~itm~ evade_condition = ~class~ evade_res = EVAL ~%resist_res%~ evade_prefix = ~D5RR~ END
	  END
	END
  END
BUT_ONLY
//___________________________________________________________________________________

END 	// end if not feats installed

END 	//	end EE-only stuff

END	//	end function

