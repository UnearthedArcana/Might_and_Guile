
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								MULTICLASS TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//COPY MARKER FILE_________________________________________________________________
//
COPY ~might_and_guile/lib/markers/d5_marker.d5~ ~override/d5__multi.d5~
//__________________________________________________________________________________


//FnP KIT DESCRIPTION PATCHING_______________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5__spheres.d5~) BEGIN
	OUTER_SPRINT focus_access @33
	OUTER_SPRINT major_access @34
	OUTER_SPRINT minor_access @35
	OUTER_SPRINT advantages @36
	OUTER_SPRINT abilities @37
	OUTER_SPRINT disadvantages @38
	OUTER_SPRINT restrictions @39
END
//__________________________________________________________________________________


//ENABLE MORE MULTICLASSES__________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	COPY_EXISTING ~CLSRCREQ.2da~ ~override~
		SET_2DA_ENTRY 34 1 8 ~1~
		SET_2DA_ENTRY 35 1 8 ~1~
		SET_2DA_ENTRY 36 1 8 ~1~
		SET_2DA_ENTRY 37 1 8 ~1~
		SET_2DA_ENTRY 46 1 8 ~1~
		SET_2DA_ENTRY 47 1 8 ~1~
		SET_2DA_ENTRY 48 1 8 ~1~
		SET_2DA_ENTRY 49 1 8 ~1~
		SET_2DA_ENTRY 50 1 8 ~1~
		SET_2DA_ENTRY 51 1 8 ~1~
//		SET_2DA_ENTRY 25 2 8 ~1~
		SET_2DA_ENTRY 35 2 8 ~1~
		SET_2DA_ENTRY 47 2 8 ~1~
		SET_2DA_ENTRY 48 2 8 ~1~
		SET_2DA_ENTRY 50 2 8 ~1~
		SET_2DA_ENTRY 51 2 8 ~1~
		SET_2DA_ENTRY 48 3 8 ~1~
		SET_2DA_ENTRY 48 4 8 ~1~
		SET_2DA_ENTRY 35 5 8 ~1~
		SET_2DA_ENTRY 48 5 8 ~1~
	BUT_ONLY
END
ACTION_IF ENGINE_IS ~tob~ THEN BEGIN
	COPY_EXISTING ~CLSRCREQ.2da~ ~override~
		SET_2DA_ENTRY 11 1 7 ~1~
		SET_2DA_ENTRY 12 1 7 ~1~
		SET_2DA_ENTRY 13 1 7 ~1~
		SET_2DA_ENTRY 14 1 7 ~1~
		SET_2DA_ENTRY 15 1 7 ~1~
		SET_2DA_ENTRY 16 1 7 ~1~
		SET_2DA_ENTRY 17 1 7 ~1~
		SET_2DA_ENTRY 18 1 7 ~1~
		SET_2DA_ENTRY 19 1 7 ~1~
		SET_2DA_ENTRY 20 1 7 ~1~
//		SET_2DA_ENTRY 5 2 7 ~1~
		SET_2DA_ENTRY 12 2 7 ~1~
		SET_2DA_ENTRY 15 2 7 ~1~
		SET_2DA_ENTRY 16 2 7 ~1~
		SET_2DA_ENTRY 18 2 7 ~1~
		SET_2DA_ENTRY 20 2 7 ~1~
		SET_2DA_ENTRY 16 3 7 ~1~
		SET_2DA_ENTRY 16 4 7 ~1~
		SET_2DA_ENTRY 12 5 7 ~1~
		SET_2DA_ENTRY 16 5 7 ~1~
	BUT_ONLY
END
//___________________________________________________________________________________


//DEAL WITH SNARES___________________________________________________________________
//
COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5cl412.spl~
  LPF ADD_SPELL_EFFECT INT_VAR target = 1 opcode = 171 timing = 1 STR_VAR resource = ~spcl412~ END

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5nx412.spl~
  LPF ADD_SPELL_EFFECT INT_VAR target = 1 opcode = 172 timing = 1 STR_VAR resource = ~spcl412~ END
  LPF ADD_SPELL_EFFECT INT_VAR target = 1 opcode = 206 parameter1 = (0 - 1) timing = 9 STR_VAR resource = ~d5cl412~ END

COPY_EXISTING ~clabth01.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL412~ ~AP_D5CL412~
IF_EXISTS BUT_ONLY
//___________________________________________________________________________________


//ADD SPELLFILCHER____________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5FILCH~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5FILCH       0 0 0 0 0 0 0 0~
~D5FILCH       0 0 0 0 0 0 0 0 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5FILCH    	   0   12  0   12  0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5FILCH           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5FILCH           0   15  0   0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5FILCH  	       0   15  0   0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5FILCH           0  0  0  1  1  1  1  1  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5FILCH           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_filch.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_MT_H K_MT_E K_MT_HE K_MT_G~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 13~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~MT0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5217
SAY @5218
SAY @5219
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5219)
		STR_VAR
			kit_name = D5FILCH
			backstab = ~1 2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4~
			clswpbon = ~0 0 3~
			thiefskl = ~50 20~
	END
END
//____________________________________________________________________________________

//SPELLFILCHER ABILITY FILES__________________________________________________________
//
COPY ~might_and_guile/mc/d5_filch.2da~ ~override~
COPY ~might_and_guile/misc/d5ssvbo.spl~ ~override~
	SAY NAME1 @15
COPY ~might_and_guile/mc/d5flch1.spl~ ~override~
	SAY NAME1 @5213
	SAY NAME2 @5213
	SAY UNIDENTIFIED_DESC @5214
COPY ~might_and_guile/mc/d5flch2.spl~ ~override~
	SAY NAME1 @5215
	SAY NAME2 @5215
	SAY UNIDENTIFIED_DESC @5216
COPY ~might_and_guile/mc/d5flch3.spl~ ~override~
COPY ~might_and_guile/mc/d5flch4.spl~ ~override~
COPY ~might_and_guile/mc/d5_flch5.spl~ ~override~
COPY ~might_and_guile/mc/d5_flch4.eff~ ~override~
COPY ~might_and_guile/mc/d5_flch5.eff~ ~override~
ACTION_IF FILE_EXISTS_IN_GAME ~d5__stats.d5~ THEN BEGIN
	COPY_EXISTING ~d5_filch.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/charisma_bonuses.txt~
		APPEND_FILE ~might_and_guile/misc/crit_changes.txt~
		APPEND_FILE ~might_and_guile/misc/intelligence_wizards.txt~
	BUT_ONLY
END
//__________________________________________________________________________________



//ADD LOREMASTER______________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5_LOREMASTER~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                      T   S           F   Q             S S
//10              L S                     W   C           L   U             W I
//9               A M             B L S   O   I   W       A   A             O N
//8               R A             A O H   H   M   A       I   R C   S       R G
//7               G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6               E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5               S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4               W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3               O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2               R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1               D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_LOREMASTER    0 0 0 0 0 0 0 0~
~D5_LOREMASTER    0 0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_LOREMASTER     0   12  0   15  0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_LOREMASTER     0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_LOREMASTER     0   17  0   17  0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_LOREMASTER     0   15  0   15  0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5_LOREMASTER     1  1  1  1  1  1  0  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5_LOREMASTER     0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_lorma.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_MT_H K_MT_E K_MT_HE K_MT_G~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 13~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ma0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5231
SAY @5232
SAY @5233
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5233)
		STR_VAR
			kit_name = D5_LOREMASTER
			backstab = ~1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1~
			clasiskl = ~0 0 10 0 0 0 0~
			clswpbon = ~0 0 3~
			thiefskl = ~30 20~
			thiefscl = ~0 100 100 100 100 100 0 0~
			traplimt = 0
			sneakatt = ~1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1~
			crippstr = ~0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	END
END
//____________________________________________________________________________________

//LOREMASTER ABILITY FILES____________________________________________________________
//
COPY ~might_and_guile/mc/d5_lorma.2da~ ~override~
COPY ~might_and_guile/misc/d5mglv1.spl~ ~override~
	SAY NAME1 @6
COPY ~might_and_guile/mc/d5lorm1.spl~ ~override~
	SAY NAME1 @4606
	SAY UNIDENTIFIED_DESC @4606
  WRITE_BYTE 0x27 0
COPY ~might_and_guile/misc/d5lor05.spl~ ~override~
	SAY NAME1 @6
//__________________________________________________________________________________



//ADD RANGER/MAGE_____________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5RANMAGE~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5RANMAGE     0 0 0 0 0 0 0 0~
~D5RANMAGE     0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 2 0 0 0 0 2 2 0 2 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANMAGE         0   12  12  12  0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANMAGE         0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANMAGE         0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANMAGE         0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5RANMAGE         1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5RANMAGE         0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_rama.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_MT_H K_MT_E K_MT_HE~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 13~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~FM0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5271
SAY @5272
SAY @5273
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5273)
		STR_VAR
			kit_name = D5RANMAGE
			backstab = ~1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1~
			clasiskl = ~0 0 0 15 15 0 0~
			clswpbon = ~1 0 3~
			thiefskl = ~30 10~
			thiefscl = ~0 0 100 100 100 100 0 0~
			traplimt = 0
			sneakatt = ~1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1~
			crippstr = ~0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
	END
END
//____________________________________________________________________________________

//RANGER/MAGE ABILITY FILES___________________________________________________________
//
COPY ~might_and_guile/mc/d5_rama.2da~ ~override~
COPY ~might_and_guile/mc/d5_nthf.spl~ ~override~
COPY ~might_and_guile/misc/d5_thac.spl~ ~override~
COPY ~might_and_guile/misc/d5_1hp.spl~ ~override~
COPY ~might_and_guile/misc/d5_2hp.spl~ ~override~
COPY ~might_and_guile/mc/d5rmcon.spl~ ~override~
COPY ~might_and_guile/misc/d5_apr7.spl~ ~override~
COPY ~might_and_guile/misc/d5_apr2.spl~ ~override~
ACTION_IF GAME_IS ~iwdee~ BEGIN	
  APPEND ~splprot.2da~ ~D5_CON_IS%TAB%41%TAB%-1%TAB%4~
  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols 
	READ_2DA_ENTRIES_NOW rows cols 
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ 
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_CON_IS~ BEGIN
	    SET con_row = %row%
	  END
	END
  BUT_ONLY
  COPY_EXISTING ~d5rmcon.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter2 = %con_row% END
END

COPY_EXISTING ~mxsplran.2da~ ~override~
  READ_2DA_ENTRY 0 0 4 cast_lev
  SET the_lev = cast_lev
BUT_ONLY
// use the_lev to patch the clab table to add 'spell slots'

// change these into innate spells and add them according to proficiency
// like a small version of multisorc
// use stat 134
COPY_EXISTING ~sppr207.spl~ ~override/D5RNM01.spl~ // goodberry
//	WRITE_SHORT 0x1c 1
//	WRITE_LONG 0x34 1
	LPM spell_to_innate
COPY_EXISTING ~sppr105.spl~ ~override/D5RNM11.spl~ // entangle
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr109.spl~ ~override/D5RNM12.spl~ // sanctuary
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr110.spl~ ~override/D5RNM13.spl~ // shillelagh
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr202.spl~ ~override/D5RNM21.spl~ // barkskin
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr212.spl~ ~override/D5RNM22.spl~ // slow poison*
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr206.spl~ ~override/D5RNM23.spl~ // flame blade
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr302.spl~ ~override/D5RNM31.spl~ // call lightning
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr317.spl~ ~override/D5RNM32.spl~ // cure disease*
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr319.spl~ ~override/D5RNM33.spl~ // summon insects
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr402.spl~ ~override/D5RNM42.spl~ // animal summ 1
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr409.spl~ ~override/D5RNM44.spl~ // death ward
	WRITE_SHORT 0x1c 1
COPY_EXISTING ~sppr501.spl~ ~override/D5RNM51.spl~ // animal summ 2
	WRITE_SHORT 0x1c 1
COPY ~might_and_guile/mc/d5rmav.spl~ ~override~
COPY ~might_and_guile/mc/d5rmave.spl~ ~override~
COPY ~might_and_guile/mc/d5rmavh.spl~ ~override~
COPY ~might_and_guile/mc/d5rmavef.eff~ ~override~
COPY ~might_and_guile/mc/d5rmavem.eff~ ~override~
COPY ~might_and_guile/mc/d5rmavhf.eff~ ~override~
COPY ~might_and_guile/mc/d5rmavhm.eff~ ~override~
//__________________________________________________________________________________



//ADD RANGER/THIEF____________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5RANTHIEF~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                    T   S           F   Q             S S
//10            L S                     W   C           L   U             W I
//9             A M             B L S   O   I   W       A   A             O N
//8             R A             A O H   H   M   A       I   R C   S       R G
//7             G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6             E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5             S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4             W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3             O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2             R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1             D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5RANTHIEF     0 0 0 0 0 0 0 0~
~D5RANTHIEF     0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANTHIEF         0   12  12  0   12  0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANTHIEF         0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANTHIEF         0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RANTHIEF         0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5RANTHIEF         1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5RANTHIEF         0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_rath.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_FT_H K_FT_E K_FT_HE~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 9~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~FT0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5371
SAY @5372
SAY @5373
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5373)
		STR_VAR
			kit_name = D5RANTHIEF
			backstab = ~1 2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4~
			clasiskl = ~0 0 0 10 10 0 0~
			clswpbon = ~1 0 3~
			thiefskl = ~30 15~
			thiefscl = ~0 0 100 100 100 100 100 0~
			sneakatt = ~1 1 1 1 1 1 1 1 2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4~
			crippstr = ~0 0 0 0 0 0 1 1 1 1 1 1 2 2 2 2 2 2 3 3 3 3 3 3 4 4 4 4 4 4 5 5 5 5 5 5 5 5 5 5 5~
	END
END
//____________________________________________________________________________________

//RANGER/THIEF ABILITY FILES__________________________________________________________
//
COPY ~might_and_guile/mc/d5_rath.2da~ ~override~
COPY ~might_and_guile/misc/d5_xp85.spl~ ~override~
COPY ~might_and_guile/mc/d5rath1.spl~ ~override~
	
COPY_EXISTING ~sppr103.spl~ ~override/d5rt103.spl~ // CLW
	LPM spell_to_innate
COPY_EXISTING ~sppr105.spl~ ~override/d5rt105.spl~ // entangle
	LPM spell_to_innate
COPY_EXISTING ~sppr106.spl~ ~override/d5rt106.spl~ // magic stone
	LPM spell_to_innate
COPY_EXISTING ~sppr109.spl~ ~override/d5rt109.spl~ // sanctuary
	LPM spell_to_innate
COPY_EXISTING ~sppr110.spl~ ~override/d5rt110.spl~ // shillelagh
	LPM spell_to_innate
COPY_EXISTING ~sppr202.spl~ ~override/d5rt202.spl~ // barkskin
	LPM spell_to_innate
COPY_EXISTING ~sppr204.spl~ ~override/d5rt204.spl~ // charm mammal
	LPM spell_to_innate
COPY_EXISTING ~sppr206.spl~ ~override/d5rt206.spl~ // flame blade
	LPM spell_to_innate
COPY_EXISTING ~sppr210.spl~ ~override/d5rt210.spl~ // resist fire/cold
	LPM spell_to_innate
COPY_EXISTING ~sppr212.spl~ ~override/d5rt212.spl~ // slow poison
	LPM spell_to_innate
COPY_EXISTING ~sppr315.spl~ ~override/d5rt315.spl~ // CMW
	LPM spell_to_innate
COPY_EXISTING ~sppr302.spl~ ~override/d5rt302.spl~ // call lightning
	LPM spell_to_innate
COPY_EXISTING ~sppr309.spl~ ~override/d5rt309.spl~ // invisibility purge
	LPM spell_to_innate
COPY_EXISTING ~sppr317.spl~ ~override/d5rt317.spl~ // cure disease
	LPM spell_to_innate
COPY_EXISTING ~sppr319.spl~ ~override/d5rt319.spl~ // summon insects
	LPM spell_to_innate
COPY ~might_and_guile/misc/d5indr1.bam~ ~override~
COPY ~might_and_guile/mc/d5rt100.spl~ ~override~
	SAY NAME1 ~Nature Magic~
	SAY UNIDENTIFIED_DESC ~Nature Magic~
	LPF ALTER_SPELL_HEADER STR_VAR icon = ~d5indr1~ END
COPY ~might_and_guile/mc/d5rt100.2da~ ~override~
COPY ~might_and_guile/misc/d5indr2.bam~ ~override~
COPY ~might_and_guile/mc/d5rt200.spl~ ~override~
	SAY NAME1 ~Nature Magic~
	SAY UNIDENTIFIED_DESC ~Nature Magic~
	LPF ALTER_SPELL_HEADER STR_VAR icon = ~d5indr2~ END
COPY ~might_and_guile/mc/d5rt200.2da~ ~override~
COPY ~might_and_guile/misc/d5indr3.bam~ ~override~
COPY ~might_and_guile/mc/d5rt300.spl~ ~override~
	SAY NAME1 ~Nature Magic~
	SAY UNIDENTIFIED_DESC ~Nature Magic~
	LPF ALTER_SPELL_HEADER STR_VAR icon = ~d5indr3~ END
COPY ~might_and_guile/mc/d5rt300.2da~ ~override~
//__________________________________________________________________________________



//ADD TOMB RUNNER______________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5_TOMBRUNNER~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                      T   S           F   Q             S S
//10              L S                     W   C           L   U             W I
//9               A M             B L S   O   I   W       A   A             O N
//8               R A             A O H   H   M   A       I   R C   S       R G
//7               G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6               E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5               S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4               W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3               O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2               R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1               D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_TOMBRUNNER    0 0 0 0 0 0 0 0~
~D5_TOMBRUNNER    0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 2 0 0 0 0 2 2 2 2 2 2 0 0 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_TOMBRUNNER     0   0   12  12  0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_TOMBRUNNER     0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_TOMBRUNNER     0   0   17  17  0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_TOMBRUNNER     0   0   15  15  0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5_TOMBRUNNER     1  1  1  1  1  1  1  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5_TOMBRUNNER     0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_tombr.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_FT_D K_FT_E K_FT_G K_FT_H K_FT_HE K_FT_HL K_FT_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 9~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~FT0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5331
SAY @5332
SAY @5333
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5333)
		STR_VAR
			kit_name = D5_TOMBRUNNER
			backstab = ~1 2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4~
			clasiskl = ~0 0 0 0 0 0 0~
			clswpbon = ~1 0 3~
			thiefskl = ~40 20~
			thiefscl = ~0 100 100 100 100 100 100 0~
			sneakatt = ~1 1 1 1 1 1 1 1 2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4 4~
			crippstr = ~0 0 0 0 0 0 1 1 1 1 1 1 2 2 2 2 2 2 3 3 3 3 3 3 4 4 4 4 4 4 5 5 5 5 5 5 5 5 5 5 5~
	END
END
//____________________________________________________________________________________

//TOMB RUNNER ABILITY FILES___________________________________________________________
//
COPY ~might_and_guile/mc/d5_tombr.2da~ ~override~
COPY ~might_and_guile/mc/d5lorm1.spl~ ~override~
COPY ~might_and_guile/misc/d5lor05.spl~ ~override~
COPY ~might_and_guile/misc/d5_ft20.spl~ ~override~
//____________________________________________________________________________________



//ADD THUG____________________________________________________________________________
//
LAF check_kit_conflict END

ADD_KIT ~D5_THUG~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                      T   S           F   Q             S S
//10              L S                     W   C           L   U             W I
//9               A M             B L S   O   I   W       A   A             O N
//8               R A             A O H   H   M   A       I   R C   S       R G
//7               G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6               E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5               S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4               W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3               O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2               R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1               D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_THUG	      0 0 0 0 0 0 0 0~
~D5_THUG	      0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_THUG	       12  0   12  0   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_THUG	       1   -2  0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_THUG	       17  0   17  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_THUG	       15  0   15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5_THUG	       0  0  0  0  1  1  0  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5_THUG	       0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/mc/d5_thug.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_FT_D K_FT_E K_FT_G K_FT_H K_FT_HE K_FT_HL K_FT_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 9~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~FT0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @5341
SAY @5342
SAY @5343
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@5343)
		STR_VAR
			kit_name = D5_THUG
			clswpbon = ~1 0 3~
			thiefskl = ~40 20~
			thiefscl = ~100 0 100 100 100 100 0 0~
			traplimt = 0
	END
END
//____________________________________________________________________________________

//THUG ABILITY FILES__________________________________________________________________
//
COPY ~might_and_guile/mc/d5_thug.2da~ ~override~
COPY ~might_and_guile/feats/d5_rfe5b.bam~ ~override~
COPY ~might_and_guile/feats/d5_rfe5.spl~ ~override~   // 			backstab bonus
	SAY NAME1 @20241
	SAY UNIDENTIFIED_DESC @20242
COPY ~might_and_guile/misc/d5ntrap.spl~ ~override~
//____________________________________________________________________________________



//MULTICLASS KIT FEATS________________________________________________________________
//
APPEND ~splprot.2da~ ~D5_KIT_IS%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_IS~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_IS~ BEGIN
	    SET kit_is_row = %row%
	  END
	END
BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~d5__rfeat.d5~) BEGIN
  COPY ~might_and_guile/feats/d5flstmc.2da~ ~override~ 

  ACTION_DEFINE_ASSOCIATIVE_ARRAY feat_nums_lets BEGIN
	1	=> a
	2	=> b
	3	=> c
	4	=> d
	5	=> e
	6	=> f
	7	=> g
	8	=> h
	9	=> i
	10	=> j
	11	=> k
	12	=> l
	13	=> m
	14	=> n
	15	=> o
	16	=> p
	17	=> q
	18	=> r
	19	=> s
	20	=> t
  END

  INCLUDE ~might_and_guile/lib/sequencer_menu.tpa~

  OUTER_SPRINT feat_select_string @20011 // ~Feat Selection~ 
  OUTER_SET feat_select_strref = RESOLVE_STR_REF (@20011) // (%feat_select_string%)
  OUTER_SPRINT feat_title_string @20012 
  OUTER_SPRINT feat_label_string @20013 

 COPY_EXISTING ~d5flstmc.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (col = 1; col < cols; ++col) BEGIN
	READ_2DA_ENTRY 0 (col - 1) (cols - 1) feat_kit_2da
	INNER_ACTION BEGIN
	  COPY_EXISTING ~kitlist.2da~ ~override~
		READ_2DA_ENTRIES_NOW rows 10
		FOR (row = 1; row < rows; ++row) BEGIN
		  READ_2DA_ENTRY_FORMER rows row 0 kitlist_index
		  READ_2DA_ENTRY_FORMER rows row 3 kitlist_name
		  READ_2DA_ENTRY_FORMER rows row 5 kitlist_2da
		  READ_2DA_ENTRY_FORMER rows row 8 kitlist_class
		  READ_2DA_ENTRY_FORMER rows row 9 kitids_num
		  PATCH_IF (~%kitlist_2da%~ STRING_EQUAL_CASE ~%feat_kit_2da%~) /*&& (IS_AN_INT %kitids_num%)*/ BEGIN
			PATCH_IF (%kitlist_class% = 3) OR (%kitlist_class% = 6) OR (%kitlist_class% = 8) OR (%kitlist_class% = 11) OR (%kitlist_class% = 12) OR (%kitlist_class% = 15) OR (%kitlist_class% = 16) OR (%kitlist_class% = 18) BEGIN
			  SET feat_spell_class = 1
			END ELSE BEGIN
			  SET feat_spell_class = 2
			END
	        SPRINT $mc_feat_kit_list(~%kitlist_index%~)~%kitids_num%~
			GET_STRREF %kitlist_name% kit_name_strref
			SPRINT feat_select_name ~%kit_name_strref% %feat_select_string%~
			SET kit_seq_name = RESOLVE_STR_REF (~%feat_select_name%~) 
			SET kit_seq_tip = RESOLVE_STR_REF (~%feat_select_name%~) 
			SET kit_seq_index = %kitlist_index% 
			INNER_ACTION BEGIN
			  LAF CREATE_SEQUENCER_MENU 
			   INT_VAR 
				name = %kit_seq_name%
				tip = %kit_seq_tip%
				desc = RESOLVE_STR_REF (~blah~) 
				column = %col% 
				class = %feat_spell_class%
			   STR_VAR 
				resref = EVAL ~d5ft%kit_seq_index%~
				icon = ~spcl919b~
				spelltable = ~d5ftskn~ 
				spelllist = ~d5flstmc~
				title = EVAL ~%feat_title_string%~
				label = EVAL ~%feat_label_string%~
			  END
			END
		  END
		END
	  BUT_ONLY
	END
  END
 BUT_ONLY

 ACTION_PHP_EACH feat_nums_lets AS num => let BEGIN
  COPY_EXISTING ~d5feat%let%.spl~ ~override~
	PHP_EACH mc_feat_kit_list AS ind => ids BEGIN
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %ids% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = EVAL ~d5ft%ind%~ END
	END
  IF_EXISTS BUT_ONLY
END

 ACTION_FOR_EACH mc_kit IN ~d5_lorma~ ~d5_rath~ ~d5_tombr~ ~d5_thug~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%mc_kit%.2da~ BEGIN
    COPY_EXISTING ~%mc_kit%.2da~ ~override~
        REPLACE_TEXTUALLY ~GA_SPCL412~ ~****      ~	
        REPLACE_TEXTUALLY ~AP_D5CL412~ ~****      ~	
        LPM remove_blank_lines
        APPEND_FILE ~might_and_guile/feats/feats_multithief.txt~
    BUT_ONLY
  END
 END

END
//____________________________________________________________________________________



//MULTICLASS KITS_____________________________________________________________________
//
LAF qd_multiclass
	STR_VAR
		kit_name = ~D5FILCH~ 
		kit_clab = ~d5_filch~
		base_class = ~T~
END 

LAF qd_multiclass
	STR_VAR
		kit_name = ~D5_LOREMASTER~ 
		kit_clab = ~d5_lorma~
		base_class = ~T~
END

LAF qd_multiclass
	STR_VAR
		kit_name = ~D5RANMAGE~ 
		kit_clab = ~d5_rama~
		base_class = ~T~
END 

LAF qd_multiclass
	STR_VAR
		kit_name = ~D5RANTHIEF~ 
		kit_clab = ~d5_rath~
		base_class = ~T~
END 

LAF qd_multiclass
	STR_VAR
		kit_name = ~D5_TOMBRUNNER~ 
		kit_clab = ~d5_tombr~
		base_class = ~T~
END 

LAF qd_multiclass
	STR_VAR
		kit_name = ~D5_THUG~ 
		kit_clab = ~d5_thug~
		base_class = ~T~
END 
//____________________________________________________________________________________


//FIX MULTICLASS KIT FEATS____________________________________________________________
//
OUTER_FOR (qd_num = 1; qd_num < 9; ++qd_num) BEGIN
  COPY_EXISTING ~qd_mct0%qd_num%.spl~ ~override~
    FOR (eff_num = 1; eff_num < 9; ++eff_num) BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 177 STR_VAR match_resource = EVAL ~d5mcft%eff_num%#~ resource = EVAL ~d5mcft%eff_num%~ END
    END
  IF_EXISTS BUT_ONLY
END
OUTER_FOR (qd_num = 10; qd_num < 50; ++qd_num) BEGIN
  COPY_EXISTING ~qd_mct%qd_num%.spl~ ~override~
    FOR (eff_num = 1; eff_num < 9; ++eff_num) BEGIN
      LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 177 STR_VAR match_resource = EVAL ~d5mcft%eff_num%#~ resource = EVAL ~d5mcft%eff_num%~ END
    END
  IF_EXISTS BUT_ONLY
END
//____________________________________________________________________________________
