
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								REVISED BEASTMASTER
//__________________________________________________________________________________
//__________________________________________________________________________________


DEFINE_ACTION_FUNCTION revised_beastmaster BEGIN


//copy marker file__________________________________________________________________
//
COPY ~might_and_guile/lib/markers/d5_marker.d5~ ~override/d5__beastmaster.d5~
//__________________________________________________________________________________


//BEASTMASTER WEAPONS____________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_SHORT 0x1c type
		PATCH_IF (type = 16) BEGIN // daggers
			READ_BYTE 0x2b kit 
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_SHORT 0x1c type 
		PATCH_IF (type = 25) BEGIN // axes
			READ_BYTE 0x2b kit
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_SHORT 0x1c type
		PATCH_IF (type = 29) BEGIN // spears
			READ_BYTE 0x2b kit 
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING ~weapprof.2da~ ~override~
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 38 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 38 3
	SET_2DA_ENTRY_LATER ~#weapprof~ 14 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 18 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 21 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 26 38 0
	SET_2DA_ENTRIES_NOW ~#weapprof~ 1
BUT_ONLY
//__________________________________________________________________________________


//UPDATE EXISTING STRINGS__________________________________________________________
// 
ACTION_FOR_EACH text_file IN ~clastext~ ~sodcltxt~ ~bgclatxt~ ~kitlist~ BEGIN
  COPY_EXISTING ~%text_file%.2da~ ~override~
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY row 0 cols kit_name
      READ_2DA_ENTRY row 1 cols kitlist_name
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~RANGER~) OR (~%kitlist_name%~ STRING_EQUAL_CASE ~RANGER~) BEGIN
        SET_2DA_ENTRY row 4 cols RESOLVE_STR_REF (@23500)
      END
    END
  IF_EXISTS BUT_ONLY
END
//__________________________________________________________________________________


//BEASTMASTER SHAPECHANGING + SPIRIT WOLF____________________________________________
//
COPY ~might_and_guile/ranger/beastmaster/d5_rnnf.spl~ ~override~
	SAY NAME1 @23501
	SAY UNIDENTIFIED_DESC @23501
COPY ~might_and_guile/ranger/beastmaster/d5_rnwlf.spl~ ~override~
	SAY NAME1 @23502
	SAY UNIDENTIFIED_DESC @23502
COPY ~might_and_guile/ranger/beastmaster/d5_rnww.spl~ ~override~
	SAY NAME1 @23503
	SAY UNIDENTIFIED_DESC @23503
COPY ~might_and_guile/ranger/beastmaster/d5_clwlf.itm~ ~override~
	SAY NAME1 @23504
	SAY UNIDENTIFIED_DESC @23504
COPY ~might_and_guile/ranger/beastmaster/d5_clww1.itm~ ~override~
	SAY NAME1 @23504
	SAY UNIDENTIFIED_DESC @23504
COPY ~might_and_guile/ranger/beastmaster/d5_clww2.itm~ ~override~
	SAY NAME1 @23504
	SAY UNIDENTIFIED_DESC @23504
COPY ~might_and_guile/ranger/beastmaster/d5_wolf.cre~ ~override~
	SAY NAME1 @23505
COPY ~might_and_guile/ranger/beastmaster/d5_dire.cre~ ~override~
	SAY NAME1 @23506
COPY ~might_and_guile/ranger/beastmaster/d5_ww01.cre~ ~override~
	SAY NAME1 @23507
COPY ~might_and_guile/ranger/beastmaster/d5_ww02.cre~ ~override~
	SAY NAME1 @23508
COPY ~might_and_guile/ranger/beastmaster/d5_ww03.cre~ ~override~
	SAY NAME1 @23509
ACTION_IF (ENGINE_IS ~tob~) BEGIN
	COPY_EXISTING ~clabrn04.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL342~ ~****      ~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/beastmaster/beastmasterog.txt~
	BUT_ONLY
END
ELSE BEGIN
	COPY_EXISTING ~clabrn04.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL342~ ~****      ~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/beastmaster/beastmaster.txt~
	BUT_ONLY
END
//__________________________________________________________________________________


END	//	end function

