
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						WARRIOR KITS
//__________________________________________________________________________________
//__________________________________________________________________________________


//_________________________CORSAIR__________________________________________________

ADD_KIT ~D5CORSA~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5CORSA       5 5 2 0 0 0 0 2~
~D5CORSA       5 5 2 0 0 0 0 2 0 5 5 2 0 1 5 5 2 2 0 0 0 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           9   12  0   9   0   9~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           15  15  0   0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           15  15  0   0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5CORSA           0  0  0  1  1  1  1  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5CORSA           0  1  1  1  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/fighter/d5_corsa.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_F_H K_F_G K_F_E K_F_HE K_F_HL K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00010000 2~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG20 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3101
SAY @3102
SAY @3103
//___________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@3104)
		STR_VAR
			kit_name = D5CORSA
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________

//CORSAIR ABILITY FILES______________________________________________________________
//
COPY ~might_and_guile/fighter/d5_corsa.2da~ ~override~
COPY ~might_and_guile/misc/d5_bsvbo.spl~ ~override~
	SAY NAME1 @17
COPY ~might_and_guile/misc/d5_luckp.spl~ ~override~
	SAY NAME1 @3108
	SAY UNIDENTIFIED_DESC @3108
COPY ~might_and_guile/fighter/d5_swapr.spl~ ~override~
COPY ~might_and_guile/fighter/d5_swash.spl~ ~override~
	SAY NAME1 @3106
	SAY UNIDENTIFIED_DESC @3107
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
  COPY_EXISTING ~d5_corsa.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/fighter/no_rogue_feats_corsair.txt~
  BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
  COPY_EXISTING ~d5_corsa.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/thief/rogue_feats_corsair.txt~
  BUT_ONLY
  ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
   COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3111)
	  END
	END
   BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
   COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3111)
	  END
	END
   BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~kitlist.2da~) BEGIN
   COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 1 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3111)
	  END
	END
   BUT_ONLY
  END
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_stats.d5~ THEN BEGIN
	COPY_EXISTING ~d5_corsa.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/charisma_bonuses.txt~
		APPEND_FILE ~might_and_guile/misc/crit_changes.txt~
	BUT_ONLY
END
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~d5_apr.d5~) OR (FILE_EXISTS_IN_GAME ~TB#TATT.spl~) THEN BEGIN
		COPY_EXISTING ~d5_corsa.2da~ ~override~
			LPM remove_blank_lines
			APPEND_FILE ~might_and_guile/profs/w1apr.txt~
			BUT_ONLY
	END
END
//___________________________________________________________________________________

//ROGUE FEATS________________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
  COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_COLS k_cols // amount of columns
	READ_2DA_ENTRIES_NOW k_rows k_cols // read all file into memory  
	FOR (k_row = 1; k_row < k_rows; ++k_row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER k_rows k_row 1 ~k_kit~ // read column value
	  PATCH_IF ~%k_kit%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET cor_row = %k_row%
	    READ_2DA_ENTRY_FORMER k_rows cor_row 9 cor_code // read column value
	  END
	END
  BUT_ONLY
  COPY_EXISTING ~d5rof01.spl~ ~override~
				~d5rof02.spl~ ~override~
				~d5rof03.spl~ ~override~
				~d5rof04.spl~ ~override~
				~d5rof05.spl~ ~override~
				~d5rof06.spl~ ~override~
				~d5rof07.spl~ ~override~
				~d5rof08.spl~ ~override~
				~d5rof09.spl~ ~override~
				~d5rof10.spl~ ~override~
				~d5rof11.spl~ ~override~
				~d5rof12.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 match_paramater1 = 16392 parameter1 = %cor_code% STR_VAR resource = ~D5_RFXC~ END
  COPY_EXISTING ~d5_rfd1.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 match_paramater1 = 16396 parameter1 = %cor_code% STR_VAR resource = ~D5_RFD2~ END
	LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 match_paramater1 = 16396 parameter1 = %cor_code% STR_VAR resource = ~D5_RFE2~ END
  COPY_EXISTING ~d5_rff1.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 177 match_paramater1 = 16396 parameter1 = %cor_code% STR_VAR resource = ~D5_RFF2~ END
END
//____________________________________________________________________________________

