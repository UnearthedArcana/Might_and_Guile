
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//									WARRIOR KITS
//__________________________________________________________________________________
//__________________________________________________________________________________


//_________________________CORSAIR__________________________________________________

ADD_KIT ~D5CORSA~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES_______________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5CORSA       0 0 0 0 0 0 0 0~
~D5CORSA       0 0 0 0 0 0 0 0 0 5 5 2 0 2 5 5 2 2 0 0 0 2 2 2 2 2 2 2 2 0 0 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           9   12  0   9   0   9~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT__________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           15  15  0   0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5CORSA           15  15  0   0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5CORSA           0  0  0  1  1  1  1  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5CORSA           0  1  1  1  0  0~

//KIT ABILITIES 2DA FILE_____________________________________________________________
~might_and_guile/fighter/d5_corsa.2da~

//RACIAL KIT AVAILABILITY____________________________________________________________
~K_F_H K_F_G K_F_E K_F_HE K_F_HL K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS__________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00010000 2~

//HIGH LEVEL ABILITIES ABBREVIATION__________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG20 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3101
SAY @3102
SAY @3103
//___________________________________________________________________________________

//EE KIT EXTRAS______________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@3104)
		STR_VAR
			kit_name = D5CORSA
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________

//CORSAIR ABILITY FILES_______________________________________________________________
//
COPY ~might_and_guile/fighter/d5_corsa.2da~ ~override~
COPY ~might_and_guile/misc/d5bsvbo.spl~ ~override~
	SAY NAME1 @17
COPY ~might_and_guile/misc/d5_acbo1.spl~ ~override~
COPY ~might_and_guile/misc/d5_jluck.spl~ ~override~
	SAY NAME1 @3108
	SAY UNIDENTIFIED_DESC @3108
COPY ~might_and_guile/fighter/d5_swapr.spl~ ~override~
COPY ~might_and_guile/misc/d5swash.spl~ ~override~				//	swashbuckling ability
	SAY NAME1 @3112
	SAY UNIDENTIFIED_DESC @3113
COPY ~might_and_guile/misc/d5swshb.eff~ ~override~
COPY ~might_and_guile/misc/d5swshc.spl~ ~override~
COPY ~might_and_guile/misc/d5swshx.spl~ ~override~
	SAY NAME1 @3114
	SAY UNIDENTIFIED_DESC @3114
COPY ~might_and_guile/misc/d5swshx.bam~ ~override~
//____________________________________________________________________________________
	

// get kit identifiers________________________________________________________________
//
OUTER_SET corsair_code = (0x4000 + %D5CORSA%)
//____________________________________________________________________________________


//FEAT SYSTEM_________________________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
  COPY_EXISTING ~d5_corsa.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/fighter/no_rogue_feats_corsair.txt~
  BUT_ONLY
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
   COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols 
	READ_2DA_ENTRIES_NOW rows cols 
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ 
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3111)
	  END
	END
   BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
   COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols 
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ 
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3111)
	  END
	END
   BUT_ONLY
  END
  COPY_EXISTING ~kitlist.2da~ ~override~
//	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows 9
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 1 ~text~
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5CORSA~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 9 RESOLVE_STR_REF (@3111)
	  END
	END
  BUT_ONLY

  INCLUDE ~might_and_guile/lib/sequencer_menu_d5.tpa~

  APPEND ~splprot.2da~ ~D5_KIT_IS%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_IS~
  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW r2en_splprot cols
	FOR (row = 1; row < r2en_splprot; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER r2en_splprot row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_IS~ BEGIN
	    SET kit_is_row = %row%
	  END
	END
  BUT_ONLY

  COPY_EXISTING ~d5_corsa.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/feats/feats_corsair.txt~
  BUT_ONLY

  OUTER_SPRINT feat_select_string @20011 // ~Feat Selection~ 
  OUTER_SET feat_select_strref = RESOLVE_STR_REF (%feat_select_string%)
  OUTER_SPRINT feat_title_string @20012 
  OUTER_SPRINT feat_label_string @20013 

  COPY_EXISTING ~kitlist.2da~ ~override~
	READ_2DA_ENTRIES_NOW r2en_kitlist 10
	FOR (row = 2; row < r2en_kitlist; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER r2en_kitlist row 5 kitlist_2da
	  PATCH_IF (~%kitlist_2da%~ STRING_EQUAL_CASE ~d5_corsa~) BEGIN
		READ_2DA_ENTRY_FORMER r2en_kitlist row 0 kitlist_index
		READ_2DA_ENTRY_FORMER r2en_kitlist row 3 kitlist_name
		READ_2DA_ENTRY_FORMER r2en_kitlist row 9 kitids_num
		GET_STRREF %kitlist_name% kit_name_strref
		SPRINT feat_select_name ~%kit_name_strref% %feat_select_string%~
		SET kit_seq_name = RESOLVE_STR_REF (~%feat_select_name%~) 
		SET kit_seq_tip = RESOLVE_STR_REF (~%feat_select_name%~) 
		SET kit_seq_index = %kitlist_index% 
		INNER_ACTION BEGIN
		  COPY_EXISTING ~d5flstf.2da~ ~override~
			COUNT_2DA_COLS cols
			COUNT_2DA_ROWS cols rows
			FOR (col = 1; col < (cols - 1); ++col) BEGIN
			  READ_2DA_ENTRY 0 (col - 1) (cols - 1) feat_kit_2da
			  PATCH_IF (~%feat_kit_2da%~ STRING_EQUAL_CASE ~d5_corsa~) BEGIN
				SET this_col = %col%
				INNER_ACTION BEGIN
				  LAF CREATE_SEQUENCER_MENU 
				   INT_VAR 
					name = %kit_seq_name%
					tip = %kit_seq_tip%
					desc = RESOLVE_STR_REF (~blah~) 
					column = %this_col% 
					class = 2
				   STR_VAR 
					resref = EVAL ~d5ft%kit_seq_index%~
					icon = ~spcl919b~
					spelltable = ~d5ftskn~ 
					spelllist = ~d5flstf~
					title = EVAL ~%feat_title_string%~
					label = EVAL ~%feat_label_string%~
				  END
				  COPY_EXISTING ~d5ft%kit_seq_index%.spl~ ~override~
					LPF DELETE_EFFECT INT_VAR match_opcode = 171 STR_VAR match_resource = EVAL ~d5ft%kit_seq_index%~ END
				  IF_EXISTS BUT_ONLY
				  ACTION_FOR_EACH letter IN ~A~ ~B~ ~C~ ~D~ ~E~ ~F~ ~G~ ~H~ ~I~ ~J~ ~K~ ~L~ ~M~ ~N~ ~O~ ~P~ ~Q~ ~R~ ~S~ ~T~ BEGIN
					COPY_EXISTING ~d5feat%letter%.spl~ ~override~
					  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %corsair_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = EVAL ~d5ft%kit_seq_index%~ END
					IF_EXISTS BUT_ONLY
				  END
				END
			  END
			END
		  BUT_ONLY
		END
	  END
	END
  BUT_ONLY

  COPY_EXISTING ~d5flstf.2da~ ~override~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR (row = 1; row < rows; ++row) BEGIN
  	  READ_2DA_ENTRY row 0 cols ~book_feat~
	  INNER_ACTION BEGIN
		COPY_EXISTING ~%book_feat%.spl~ ~override/%book_feat%L.spl~
		  WRITE_SHORT 0x1c 4
		  WRITE_LONG 0x34 1
		  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 171 target = 1 timing = 9 STR_VAR resource = EVAL ~%book_feat%~ END
		IF_EXISTS BUT_ONLY
	  END
	END
  BUT_ONLY

END
//___________________________________________________________________________________

