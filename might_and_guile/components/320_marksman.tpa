
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						  		MARKSMAN
//__________________________________________________________________________________
//__________________________________________________________________________________


//ADD MARKSMAN_______________________________________________________________________

ADD_KIT ~D5MARKS~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES_______________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5MARKS       0 0 0 0 0 0 0 0~
~D5MARKS       0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 5 5 5 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MARKS           9   9   0   0   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MARKS           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT__________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MARKS           15  15  0   0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MARKS           15  15  0   0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5MARKS           1  1  1  1  1  1  1  1  1~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5MARKS           0  1  1  1  1  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/fighter/d5_marks.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_F_H K_F_G K_F_D K_F_HE K_F_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x40000000 2~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Fi0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG24 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3202
SAY @3203
SAY @3204
//___________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@3205)
		STR_VAR
			kit_name = D5MARKS
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________

//MARKSMAN ABILITY FILES______________________________________________________________
//
COPY ~might_and_guile/fighter/d5_marks.2da~ ~override~

COPY ~might_and_guile/misc/d5_mthac.spl~ ~override~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_archy.d5~ BEGIN
	INCLUDE ~might_and_guile/lib/archery_feats.tpa~
END
//___________________________________________________________________________________

//NO FEATS___________________________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
 COPY_EXISTING ~d5_marks.2da~ ~override~
  LPM remove_blank_lines
  APPEND_FILE ~might_and_guile/ranger/called_shots.txt~
 BUT_ONLY
END
//___________________________________________________________________________________

//FEAT SYSTEM_________________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN
  COPY_EXISTING ~d5_marks.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/feats/feats_archer.txt~
  BUT_ONLY
  ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
   COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5MARKS~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3206)
	  END
	END
   BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
   COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5MARKS~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3206)
	  END
	END
   BUT_ONLY
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~kitlist.2da~) BEGIN
   COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 1 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~D5MARKS~ BEGIN
	    SET patch_row = %row%
		SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@3206)
	  END
	END
   BUT_ONLY
  END
  EXTEND_TOP ~d5_feat.bcs~ ~might_and_guile/feats/d5_marks.baf~
  COMPILE ~might_and_guile/feats/d5_archr.d~
END
//___________________________________________________________________________________

//DON'T USE SCROLLS/WANDS___________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rf84n.spl~ BEGIN
	APPEND ~d5_marks.2da~ ~ABILITY    AP_D5_RF84N ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~
	APPEND ~d5_marks.2da~ ~ABILITY    AP_D5_RF83N ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~
END
//__________________________________________________________________________________

//FIX BARB USABILITY__________________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x1f "use"
		READ_BYTE 0x29 "kit"
		PATCH_IF ((("%kit%" BAND 0b01000000) = 0b01000000) AND 
				(("%use%" BAND 0b00001000) = 0b00001000)) THEN BEGIN
			WRITE_BYTE 0x29 ("%kit%" BAND 0b10111111) 
		END
	END
BUT_ONLY_IF_IT_CHANGES
//____________________________________________________________________________________
