
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								WARRIOR KITS
//__________________________________________________________________________________
//__________________________________________________________________________________


//______________________BARBARIAN RANGER__________________________________________
//
ADD_KIT ~D5RBARB~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5RBARB       0 0 0 0 0 0 0 0~
~D5RBARB       0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RBARB           12  0   12  0   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RBARB           0   0   0   0   0   -2~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RBARB           15  0   15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5RBARB           15  0   15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5RBARB           1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5RBARB           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/ranger/d5_rbarb.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_R_H K_R_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x40000000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~CHAN09	* HELM07 BAG20 RING06 RING31 * BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H27 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3604
SAY @3605
SAY @3606
//___________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
// 
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@3607)
		STR_VAR
			kit_name = D5RBARB
			clswpbon = ~1 0 3~
	END
END
//____________________________________________________________________________________

//BARBARIAN RANGER ABILITY FILES___________________________________________________
//
COPY ~might_and_guile/ranger/d5_rbarb.2da~ ~override~
COPY ~might_and_guile/ranger/d5_rbaab.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rback.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rnosp.eff~ ~override~
COPY ~might_and_guile/ranger/d5_rnosp.spl~ ~override~
ACTION_IF FILE_EXISTS_IN_GAME ~d5_strid.d5~ THEN BEGIN
	COPY_EXISTING ~d5_rbarb.2da~ ~override~
		REPLACE_TEXTUALLY ~AP_SPCL151~ ~****      ~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/stribarb.txt~
		BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_stats.d5~ THEN BEGIN
	COPY_EXISTING ~d5_rbarb.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/charisma_bonuses.txt~
		APPEND_FILE ~might_and_guile/misc/crit_changes.txt~
	BUT_ONLY
END
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~d5_rbarb.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/tracking.txt~
		BUT_ONLY
END
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~d5_apr.d5~) OR (FILE_EXISTS_IN_GAME ~TB#TATT.spl~) THEN BEGIN
		COPY_EXISTING ~d5_rbarb.2da~ ~override~
			LPM remove_blank_lines
			APPEND_FILE ~might_and_guile/profs/w1apr.txt~
			BUT_ONLY
	END
END
//____________________________________________________________________________________


// get kit identifiers________________________________________________________________
//
OUTER_SET barb_ranger_code = (0x4000 + %D5RBARB%)
//____________________________________________________________________________________


//FEAT SYSTEM_________________________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rfeat.d5~ THEN BEGIN

  INCLUDE ~might_and_guile/lib/sequencer_menu_d5.tpa~

  APPEND ~splprot.2da~ ~D5_KIT_IS%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_IS~
  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW r2en_splprot cols
	FOR (row = 1; row < r2en_splprot; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER r2en_splprot row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_IS~ BEGIN
	    SET kit_is_row = %row%
	  END
	END
  BUT_ONLY

  COPY_EXISTING ~d5_rbarb.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~might_and_guile/feats/feats_ranger.txt~
  BUT_ONLY

  OUTER_SPRINT feat_select_string @20011 // ~Feat Selection~ 
  OUTER_SET feat_select_strref = RESOLVE_STR_REF (%feat_select_string%)
  OUTER_SPRINT feat_title_string @20012 
  OUTER_SPRINT feat_label_string @20013 

  COPY_EXISTING ~kitlist.2da~ ~override~
	READ_2DA_ENTRIES_NOW r2en_kitlist 10
	FOR (row = 2; row < r2en_kitlist; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER r2en_kitlist row 5 kitlist_2da
	  PATCH_IF (~%kitlist_2da%~ STRING_EQUAL_CASE ~d5_rbarb~) BEGIN
		READ_2DA_ENTRY_FORMER r2en_kitlist row 0 kitlist_index
		READ_2DA_ENTRY_FORMER r2en_kitlist row 3 kitlist_name
		READ_2DA_ENTRY_FORMER r2en_kitlist row 9 kitids_num
		GET_STRREF %kitlist_name% kit_name_strref
		SPRINT feat_select_name ~%kit_name_strref% %feat_select_string%~
		SET kit_seq_name = RESOLVE_STR_REF (~%feat_select_name%~) 
		SET kit_seq_tip = RESOLVE_STR_REF (~%feat_select_name%~) 
		SET kit_seq_index = %kitlist_index% 
		INNER_ACTION BEGIN
		  COPY_EXISTING ~d5flstf.2da~ ~override~
			COUNT_2DA_COLS cols
			COUNT_2DA_ROWS cols rows
			FOR (col = 1; col < (cols - 1); ++col) BEGIN
			  READ_2DA_ENTRY 0 (col - 1) (cols - 1) feat_kit_2da
			  PATCH_IF (~%feat_kit_2da%~ STRING_EQUAL_CASE ~d5_rbarb~) BEGIN
				SET this_col = %col%
				INNER_ACTION BEGIN
				  LAF CREATE_SEQUENCER_MENU 
				   INT_VAR 
					name = %kit_seq_name%
					tip = %kit_seq_tip%
					desc = RESOLVE_STR_REF (~blah~) 
					column = %this_col% 
					class = 1
				   STR_VAR 
					resref = EVAL ~d5ft%kit_seq_index%~
					icon = ~spcl919b~
					spelltable = ~d5ftskn~ 
					spelllist = ~d5flstf~
					title = EVAL ~%feat_title_string%~
					label = EVAL ~%feat_label_string%~
				  END
				  COPY_EXISTING ~d5ft%kit_seq_index%.spl~ ~override~
					LPF DELETE_EFFECT INT_VAR match_opcode = 171 STR_VAR match_resource = EVAL ~d5ft%kit_seq_index%~ END
				  IF_EXISTS BUT_ONLY
				  ACTION_FOR_EACH letter IN ~A~ ~B~ ~C~ ~D~ ~E~ ~F~ ~G~ ~H~ ~I~ ~J~ ~K~ ~L~ ~M~ ~N~ ~O~ ~P~ ~Q~ ~R~ ~S~ ~T~ BEGIN
					COPY_EXISTING ~d5feat%letter%.spl~ ~override~
					  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %barb_ranger_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = EVAL ~d5ft%kit_seq_index%~ END
					IF_EXISTS BUT_ONLY
				  END
				END
			  END
			END
		  BUT_ONLY
		END
	  END
	END
  BUT_ONLY
  COPY_EXISTING ~d5flstf.2da~ ~override~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR (row = 1; row < rows; ++row) BEGIN
  	  READ_2DA_ENTRY row 0 cols ~book_feat~
	  INNER_ACTION BEGIN
		COPY_EXISTING ~%book_feat%.spl~ ~override/%book_feat%L.spl~
		  WRITE_SHORT 0x1c 4
		  WRITE_LONG 0x34 1
		  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 171 target = 1 timing = 9 STR_VAR resource = EVAL ~%book_feat%~ END
		IF_EXISTS BUT_ONLY
	  END
	END
  BUT_ONLY

END
//____________________________________________________________________________________


//DEFINE SPHERE ACCESS______________________________________________________________
//
ACTION_INCLUDE ~%MOD_FOLDER%/lib/fnp_compat.tpa~

COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 1 rows
	READ_2DA_ENTRY (%rows% - 1) 5 1 mod_clab
BUT_ONLY

LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
	u_leather_armor 	= 1
	u_chain_armor 		= 1
	u_plate_armor 		= 0
	u_basic_weapons 	= 2
	u_mace_hammer 		= 2
	u_flails 			= 2
	u_axes 				= 2
	u_medium_swords 	= 2
	u_curved_swords 	= 0
	u_large_blades 		= 2
	u_spears 			= 2
	u_darts 			= 0
	u_bows 				= 2
	u_crossbows 		= 0  
  STR_VAR
    clab_name 	= EVAL ~%mod_clab%~
	class 			= ~scout~
	s_Life 			= ~ranger~
	s_Death 		= ~x~
	s_Benediction 	= ~x~
	s_Destruction 	= ~x~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Exploration   = ~ranger~
	s_Knowledge 	= ~x~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~x~
	s_Vigor 		= ~ranger~
	s_Affliction 	= ~x~
	s_Animal 		= ~x~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
	s_Perdition     = ~x~
END

LAM ~READ_FNP_KIT_INFO~

ACTION_IF FILE_EXISTS_IN_GAME ~d5_spheres.d5~ BEGIN
  LAM apply_fnp_spheres
END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~ BEGIN
  LAM apply_fnp_usability
END
//__________________________________________________________________________________


/*
	WHAT WAS THE PURPOSE OF THIS???
//FIX BARB USABILITY__________________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_BYTE 0x1f "use"
		READ_BYTE 0x29 "kit"
		PATCH_IF ((("%kit%" BAND 0b01000000) = 0b01000000) AND 
				(("%use%" BAND 0b00001000) = 0b00001000)) THEN BEGIN
			WRITE_BYTE 0x29 ("%kit%" BAND 0b10111111) 
		END
	END
BUT_ONLY_IF_IT_CHANGES
//____________________________________________________________________________________
*/
