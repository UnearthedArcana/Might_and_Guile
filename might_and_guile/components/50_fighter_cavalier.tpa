
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								WARRIOR TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//______________________FIGHTER_CAVALIER____________________________________________

DEFINE_ACTION_FUNCTION fighter_cavalier BEGIN

//remove from paladin menus_________________________________________________________
// 
LAF remove_menu_kit INT_VAR del_kit = 4 STR_VAR class = ~P~ END

//add to fighter menus______________________________________________________________
// 
ACTION_FOR_EACH race IN ~D~ ~E~ ~G~ ~H~ ~HE~ ~HL~ ~HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~K_F_%race%.2da~ BEGIN
	  COPY_EXISTING ~K_F_%race%.2da~ ~override~
		COUNT_2DA_COLS cols
		COUNT_2DA_ROWS cols rows
		PATCH_IF (rows > 3) BEGIN
		  INSERT_2DA_ROW 3 cols ~3    4~
		END
	  BUT_ONLY
	END
END

//allow non-humans__________________________________________________________________
// 
COPY_EXISTING ~clsrcreq.2da~ ~override~
	COUNT_2DA_ROWS 8 rows
	FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 0 8 entry
      PATCH_IF (~%entry%~ STRING_EQUAL_CASE ~CAVALIER~) BEGIN
    	SET_2DA_ENTRY  row 2 8 1
    	SET_2DA_ENTRY  row 3 8 1
    	SET_2DA_ENTRY  row 4 8 1
    	SET_2DA_ENTRY  row 5 8 1
    	SET_2DA_ENTRY  row 6 8 1
    	SET_2DA_ENTRY  row 7 8 1
      END
	END
	PRETTY_PRINT_2DA
IF_EXISTS BUT_ONLY


//change class in kitlist___________________________________________________________
// 
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 1 9 kitname
    READ_2DA_ENTRY row 8 9 kitclass
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~CAVALIER~) BEGIN
      SET_2DA_ENTRY row 8 9 2
    END
  END
BUT_ONLY

//remove some paladin abilities_____________________________________________________
// 
COPY_EXISTING ~clabpa02.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL211~ ~****      ~
  REPLACE_TEXTUALLY ~GA_SPCL212~ ~****      ~
  REPLACE_TEXTUALLY ~GA_SPCL213~ ~****      ~
IF_EXISTS BUT_ONLY

//allow cavalier dual-class_________________________________________________________
// 
COPY_EXISTING ~dualclas.2da~ ~override~
  COUNT_2DA_ROWS 7 rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 0 7 kitname
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~CAVALIER~) BEGIN
      SET cav_row = %row%
    END
  END
  PATCH_IF (VARIABLE_IS_SET %cav_row%) BEGIN
    SET_2DA_ENTRY cav_row 2 7 1
    SET_2DA_ENTRY cav_row 3 7 1
    SET_2DA_ENTRY cav_row 4 7 1
    SET_2DA_ENTRY cav_row 5 7 1
  END
BUT_ONLY

//allow grandmastery________________________________________________________________
// 
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 1; row < rows; ++row ) BEGIN
    READ_2DA_ENTRY row 1 9 kitname
    READ_2DA_ENTRY row 6 9 profcol
    PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~CAVALIER~) BEGIN
      SET cav_profs = (%profcol% + 1)
    END
  END
BUT_ONLY

COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 0; row < 27; ++row ) BEGIN
    READ_2DA_ENTRY row cav_profs cols profnum
    PATCH_IF (profnum > 0) BEGIN
      SET_2DA_ENTRY row cav_profs cols 5
    END
  END
BUT_ONLY

END	//	end function

