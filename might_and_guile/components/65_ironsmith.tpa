
//_________________________IRONSMITH________________________________________________
//
DEFINE_ACTION_FUNCTION ironsmith_kit BEGIN

LAF check_kit_conflict END

LAF ADD_KIT_EX 
  INT_VAR
    kit_class  = 2
    lower      = RESOLVE_STR_REF(@6501)
    mixed      = RESOLVE_STR_REF(@6502)
    help       = RESOLVE_STR_REF(@6503)
    briefdesc  = RESOLVE_STR_REF(@6503)
  STR_VAR
    kit_name   = ~D5_IRONSMITH~
    unusable   = ~0x00004000~
    clasweap   = ~1 1 1 1 0 1 1 0~
//11                                      T   S           F   Q             S S
//10              L S                     W   C           L   U             W I
//9               A M             B L S   O   I   W       A   A             O N
//8               R A             A O H   H   M   A       I   R C   S       R G
//7               G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6               E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5               S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4               W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3               O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2               R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1               D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
    weapprof   = ~0 0 0 0 0 0 0 0 3 3 3 5 3 3 3 3 5 3 3 3 3 3 3 3 3 3 3 3 0 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
//                ST DX CO IN WI CH
    abclasrq   = ~14 0  12 0  0  0~
    abclsmod   = ~0  0  0  0  0  0~
    abdcdsrq   = ~17 0  0  0  0  0~
    abdcscrq   = ~15 0  0  0  0  0~
//                LG LN LE NG TN NE CG CN CE
    alignmnt   = ~1  1  1  1  1  1  1  1  1~
//                F C M T D R
    dualclas   = ~1 1 0 1 0 0~
    luabbr     = ~Fi0~
    stweap     = ~LEAT14	* HELM07 BAG20 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~
    clab_path  = ~might_and_guile/fighter/smith/d5smith.2da~
    kittable   = ~K_F_H K_F_G K_F_E K_F_HE K_F_HL K_F_HO~
  RET D5_IRONSMITH = kit_id
END
//____________________________________________________________________________________

OUTER_SET ironsmith_kit_code = (0x4000 + %D5_IRONSMITH%)

//IRONSMITH ABILITY FILES_______________________________________________________________
//
COPY ~might_and_guile/fighter/smith/d5smith.2da~ ~override~

ACTION_IF (GAME_IS ~iwd~) BEGIN
  COPY_EXISTING ~sppr213.spl~ ~override/d5smth3.spl~
    LPM spell_to_innate
  COPY_EXISTING ~d5smith.2da~ ~override~
    REPLACE_TEXTUALLY ~GA_SPIN113~ ~GA_D5SMTH3~
  IF_EXISTS BUT_ONLY
END

// add resistance to Commands/PWs - d5smth4

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5smthev.2da~) BEGIN
<<<<<<<< d5/d5smthev.2da
2DA V1.0 
RES
SPPR203D 	SPL
>>>>>>>> 
  COPY ~d5/d5smthev.2da~ ~override/d5smthev.2da~ 
END

ACTION_FOR_EACH smith_evade_spell IN 
  ~CLERIC_COMMAND~
  ~CLERIC_GREATER_COMMAND~
  ~CLERIC_HOLY_WORD~
  ~CLERIC_UNHOLY_WORD~
  ~WIZARD_POWER_WORD_SLEEP~
  ~WIZARD_SOUND_BURST~
  ~WIZARD_POWER_WORD_SILENCE~
  ~WIZARD_POWER_WORD_STUN~
  ~WIZARD_POWER_WORD_BLIND~
  ~WIZARD_POWER_WORD_KILL~
BEGIN
  OUTER_SET spell_ids = IDS_OF_SYMBOL (~spell~ ~%smith_evade_spell%~)
  ACTION_IF !(spell_ids = 0 - 1) BEGIN
    LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL ~%smith_evade_spell%~ RET spell_res END
    APPEND ~d5smthev.2da~ ~%spell_res% 	SPL~ UNLESS ~%spell_res%~
  END
END

COPY_EXISTING ~d5smthev.2da~ ~override~ 
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 cols smith_evade_res
	READ_2DA_ENTRY row 1 cols smith_evade_type
	PATCH_IF (~%smith_evade_type%~ STRING_EQUAL_CASE ~spl~) BEGIN 
	  PATCH_IF FILE_EXISTS_IN_GAME ~%smith_evade_res%.spl~ BEGIN
	    INNER_ACTION BEGIN
	 	  LAF add_evade_spell INT_VAR evasion_kit = %ironsmith_kit_code% evasion_save = 0 evasion_chance = 50 STR_VAR evade_ext = ~spl~ evade_condition = ~kit~ evade_res = EVAL ~%smith_evade_res%~ evade_prefix = ~D5WE~ END
	    END
	  END
	END
  END
BUT_ONLY

// add resounding smite - d5smth5

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5smth5.spl~			//	resounding strike
  SAY NAME1 @6506
  SAY UNIDENTIFIED_DESC @6507
  WRITE_ASCII 0x3a ~spwi223c~ #8
  LPF ALTER_SPELL_HEADER INT_VAR target = 7 STR_VAR icon = ~spwi223b~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 1 parameter2 = 146 timing = 0 duration = 126144000 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 248 target = 1 timing = 0 duration = 126144000 STR_VAR resource = ~d5smth5a~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 248 target = 1 timing = 0 duration = 126144000 STR_VAR resource = ~d5smth5x~ END

CREATE EFF ~d5smth5a~
	WRITE_LONG 0x10 146
	WRITE_LONG 0x14 2
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 1
	WRITE_LONG 0x24 1
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5reson~ #8

ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsburst.vvc~) BEGIN
  COPY ~might_and_guile/fighter/smith/dvsburst.vvc~ ~override~
END
ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsburst.bam~) BEGIN
  COPY ~might_and_guile/fighter/smith/dvsburst.bam~ ~override~
END

ADD_PROJECTILE ~might_and_guile/fighter/smith/d5sburst.pro~

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5reson.spl~) BEGIN
  COPY ~might_and_guile/fighter/smith/d5reson.spl~ ~override~
END
COPY_EXISTING ~d5reson.spl~ ~override~
  LPF ALTER_SPELL_HEADER INT_VAR projectile = %d5sburst% END
  LPF DELETE_EFFECT INT_VAR match_opcode = 80 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 80 target = 2 timing = 0 duration = 30 savingthrow = 16 END

CREATE EFF ~d5smth5x~
	WRITE_LONG 0x10 146
	WRITE_LONG 0x14 9
	WRITE_LONG 0x1c 0
	WRITE_LONG 0x20 1
	WRITE_LONG 0x24 1
	WRITE_SHORT 0x2c 100
	WRITE_ASCII 0x30 ~d5smth5x~ #8

COPY ~%MOD_FOLDER%/lib/d5_base.spl~ ~override/d5smth5x.spl~
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5smth5~ END  

// ***** set up another ability... Enchant Weapon/Armor?? Or, maybe don't need? Something defensive, about well-maintained armor?

//____________________________________________________________________________________
	
//WILD TALENT_________________________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5__psionics.d5~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~d5smith.2da~) BEGIN
	APPEND ~d5smith.2da~ ~WILD_TLNT   AP_D5PSWT_  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~
  END
END
//____________________________________________________________________________________

END	//	end function


//____________________________________________________________________________________
//____________________________________________________________________________________


DEFINE_ACTION_FUNCTION ironsmith_breagar BEGIN

COPY_EXISTING ~kit.ids~ ~override~
  READ_2DA_ENTRIES_NOW r2en_kitids 2
  FOR (kitids_row = 1; kitids_row < r2en_kitids; ++kitids_row) BEGIN
    READ_2DA_ENTRY_FORMER r2en_kitids kitids_row 1 kit_name
    PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~D5_IRONSMITH~) BEGIN
      READ_2DA_ENTRY_FORMER r2en_kitids kitids_row 0 ironsmith_code
    END
  END
BUT_ONLY

ACTION_IF (VARIABLE_IS_SET %ironsmith_code%) BEGIN
  ACTION_FOR_EACH breagar IN ~acbre~ ~acbre07~ ~acbre25~ BEGIN
    COPY_EXISTING ~%breagar%.cre~ ~override~
      WRITE_LONG 0x244 (%ironsmith_code%) << 16
    BUT_ONLY
  END
END

END	//	end function


//____________________________________________________________________________________
//____________________________________________________________________________________

