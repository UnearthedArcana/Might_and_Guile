
//__________________________RAKE____________________________________________________
//
DEFINE_ACTION_FUNCTION rake_kit BEGIN

LAF check_kit_conflict END

LAF ADD_KIT_EX 
  INT_VAR
    kit_class  = 4
    lower      = RESOLVE_STR_REF(@9002)
    mixed      = RESOLVE_STR_REF(@9003)
    help       = RESOLVE_STR_REF(@9004)
    briefdesc  = RESOLVE_STR_REF(@9004)
  STR_VAR
    kit_name   = ~D5_RAKE~
    unusable   = ~0x00004000~
    clasweap   = ~1 1 1 1 1 0 0 0~
//11                                      T   S           F   Q             S S
//10              L S                     W   C           L   U             W I
//9               A M             B L S   O   I   W       A   A             O N
//8               R A             A O H   H   M   A       I   R C   S       R G
//7               G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6               E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5               S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4               W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3               O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2               R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1               D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
    weapprof   = ~0 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 2 0 0 0 0 2 2 0 2 2 2 0 0 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
//                ST DX CO IN WI CH
    abclasrq   = ~9  15 0  9  0  12~
    abclsmod   = ~0  0  0  0  0  0~
    abdcdsrq   = ~0  17 0  0  0  0~
    abdcscrq   = ~0  15 0  0  0  0~
//                LG LN LE NG TN NE CG CN CE
    alignmnt   = ~0  0  0  1  1  1  1  1  1~
//                F C M T D R
    dualclas   = ~1 1 1 0 0 0~
    luabbr     = ~Th0~
    stweap     = ~LEAT14	* HELM07 BAG20 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 HAMM07 SW1H39 STAF08~
    clab_path  = ~might_and_guile/thief/rake/d5_rake.2da~
    kittable   = ~K_T_H K_T_G K_T_E K_T_HE K_T_HL K_T_HO~
    thiefskl   = ~40 20~
    thiefscl   = ~100 100 100 100 100 100 100 0~
    backstab   = ~1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2~
  RET D5_RAKE = kit_id
END
//__________________________________________________________________________________

//RAKE & SWASHBUCKLER ABILITY FILES_________________________________________________
//
COPY ~might_and_guile/thief/rake/d5_rake.2da~ ~override~
COPY ~might_and_guile/misc/d5luckp.spl~ ~override~
	SAY NAME1 @5508
	SAY UNIDENTIFIED_DESC @5508
//***** thac0 and AC bonus, and add to the CLAB (or just use the swashy ones)
//__________________________________________________________________________________

//WILD TALENT_______________________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5__psionics.d5~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~d5_rake.2da~) BEGIN
	APPEND ~d5_rake.2da~ ~WILD_TLNT   AP_D5PSWT_  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~
  END
END
//__________________________________________________________________________________

//UPDATE SWASHBUCKLER_______________________________________________________________
// 
COPY_EXISTING ~clabth04.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL412~ ~****      ~

APPEND ~backstab.2da~ ~SWASHBUCKLER  1             2             2             2             2             2             2             2             2             3             3             3             3             3             3             3             3             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4             4 ~  

COPY_EXISTING ~thiefskl.2da~ ~override~
  COUNT_2DA_ROWS 3 rows
  FOR (row = 1; row < rows; ++row) BEGIN
    READ_2DA_ENTRY row 0 3 kit_name
    PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~SWASHBUCKLER~) BEGIN
      SET_2DA_ENTRY row 1 3 ~30~
      SET_2DA_ENTRY row 2 3 ~20~
    END
  END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~dualclas.2da~ ~override~
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 1; row < rows; ++row) BEGIN
    READ_2DA_ENTRY row 0 cols kit_name
    PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~SWASHBUCKLER~) BEGIN
      SET_2DA_ENTRY row 1 cols ~0~
    END
  END
IF_EXISTS BUT_ONLY
//__________________________________________________________________________________

//UPDATE EXISTING STRINGS___________________________________________________________
// 
ACTION_FOR_EACH text_file IN ~clastext~ ~sodcltxt~ ~bgclatxt~ ~kitlist~ BEGIN
  COPY_EXISTING ~%text_file%.2da~ ~override~
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY row 0 cols kit_name
      READ_2DA_ENTRY row 1 cols kitlist_name
      PATCH_IF (~%kit_name%~ STRING_EQUAL_CASE ~SWASHBUCKLER~) OR (~%kitlist_name%~ STRING_EQUAL_CASE ~SWASHBUCKLER~) BEGIN
        SET_2DA_ENTRY row 4 cols RESOLVE_STR_REF (@23500)
      END
    END
  IF_EXISTS BUT_ONLY
END
//__________________________________________________________________________________

//______________________HLAS________________________________________________________
//
ACTION_IF !(FILE_EXISTS_IN_GAME ~TG#TOUG.spl~) BEGIN
	COPY_EXISTING ~LUTh0.2da~ ~override/luth4.2da~	
END
//__________________________________________________________________________________

END	//	end function

