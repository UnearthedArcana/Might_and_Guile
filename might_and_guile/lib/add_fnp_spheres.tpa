
//ADD FnP SPHERES to KIT_____________________________________________________________
//
  COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 4; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 5 ~clab~ // read column value
	  PATCH_IF ~%clab%~ STRING_EQUAL_CASE ~%mod_clab%~ BEGIN
		SET kit_index = %row%
		READ_2DA_ENTRY_FORMER rows kit_index 5 kit_clab
		READ_2DA_ENTRY_FORMER rows kit_index 1 kit_name
		READ_2DA_ENTRY_FORMER rows kit_index 4 kit_desc
		READ_2DA_ENTRY_FORMER rows kit_index 9 kit_code
		READ_2DA_ENTRY_FORMER rows kit_index 8 kit_class
	  END
	END
  BUT_ONLY
  ACTION_IF IS_AN_INT ~%kit_class%~ BEGIN
	ACTION_PHP_EACH d5_fnp_spheres AS sphere => val BEGIN
	  ACTION_IF (VARIABLE_IS_SET $d5_fnp_kit_sphere_access(~%sphere%~ ~%kit_clab%~)) BEGIN
		OUTER_TEXT_SPRINT access $d5_fnp_kit_sphere_access(~%sphere%~ ~%kit_clab%~)
		ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && ~%access%~ STRING_EQUAL_CASE ~FOCUS~) BEGIN
		  COPY_EXISTING ~lub_%kit_index%.2da~ ~override~
			APPEND_FILE ~faiths_and_powers/spheres/hlas/%sphere%/hlas_%sphere%.txt~
		  BUT_ONLY
		  COPY_EXISTING ~%kit_clab%.2da~ ~override~ 
			LPM remove_blank_lines
			APPEND_FILE ~faiths_and_powers/spheres/clab/%sphere%_focus.txt~
		  BUT_ONLY
		  OUTER_TEXT_SPRINT focus_sphere_list ~%focus_sphere_list% %sphere%~
		END
		ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && ~%access%~ STRING_EQUAL_CASE ~MAJOR~) BEGIN
		  COPY_EXISTING ~lub_%kit_index%.2da~ ~override~
			APPEND_FILE ~faiths_and_powers/spheres/hlas/%sphere%/hlas_%sphere%.txt~
		  BUT_ONLY
		  COPY_EXISTING ~%kit_clab%.2da~ ~override~ 
			LPM remove_blank_lines
			APPEND_FILE ~faiths_and_powers/spheres/clab/%sphere%_major.txt~
		  BUT_ONLY
		  OUTER_TEXT_SPRINT major_sphere_list ~%major_sphere_list% %sphere%,~
		END
		ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && ~%access%~ STRING_EQUAL_CASE ~MINOR~) BEGIN
		  COPY_EXISTING ~%kit_clab%.2da~ ~override~ 
			LPM remove_blank_lines
			APPEND_FILE ~faiths_and_powers/spheres/clab/%sphere%_minor.txt~
		  BUT_ONLY
		  OUTER_TEXT_SPRINT minor_sphere_list ~%minor_sphere_list% %sphere%,~
		END
		ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && ~%access%~ STRING_EQUAL_CASE ~PALADIN~) BEGIN
		  COPY_EXISTING ~%kit_clab%.2da~ ~override~ 
			LPM remove_blank_lines
			APPEND_FILE ~faiths_and_powers/spheres/clab/%sphere%_paladin.txt~
		  BUT_ONLY
		  OUTER_TEXT_SPRINT minor_sphere_list ~%minor_sphere_list% %sphere%,~
		END
		ACTION_IF (VARIABLE_IS_SET $d5_fnp_sphere_access(~%access%~) && ~%access%~ STRING_EQUAL_CASE ~RANGER~) BEGIN
		  COPY_EXISTING ~%kit_clab%.2da~ ~override~ 
			LPM remove_blank_lines
			APPEND_FILE ~faiths_and_powers/spheres/clab/%sphere%_ranger.txt~
		  BUT_ONLY
		  OUTER_TEXT_SPRINT minor_sphere_list ~%minor_sphere_list% %sphere%,~
		END
	  END
	END
	ACTION_IF (%kit_class% = 3) OR  
  			(%kit_class% = 11) OR  
  			(%kit_class% = 8) OR  
  			(%kit_class% = 14) OR  
  			(%kit_class% = 15) OR  
  			(%kit_class% = 18) BEGIN
	  COPY_EXISTING ~luabbr.2da~ ~override~
		REPLACE_TEXTUALLY ~^\(%kit_name%[ %TAB%]+\).+$~ ~\1b_%kit_index%~
	  BUT_ONLY
	END
	ACTION_IF NOT (~%focus_sphere_list%~ STRING_EQUAL_CASE ~~) BEGIN
		OUTER_TEXT_SPRINT focus_sphere_list ~%WNL%%focus_access%%focus_sphere_list%~
	END
	OUTER_TEXT_SPRINT total_sphere_list ~%total_sphere_list%%focus_sphere_list%~
	ACTION_IF NOT (~%major_sphere_list%~ STRING_EQUAL_CASE ~~) BEGIN
		OUTER_TEXT_SPRINT major_sphere_list ~%WNL%%major_access%%major_sphere_list%~
	END
	OUTER_TEXT_SPRINT total_sphere_list ~%total_sphere_list%%major_sphere_list%~
	ACTION_IF NOT (~%minor_sphere_list%~ STRING_EQUAL_CASE ~~) BEGIN
		OUTER_TEXT_SPRINT minor_sphere_list ~%WNL%%minor_access%%minor_sphere_list%~
	END
	OUTER_TEXT_SPRINT total_sphere_list ~%total_sphere_list%%minor_sphere_list%~
	OUTER_TEXT_SPRINT kit_sphere_list ~Abilities: %total_sphere_list%~
	ACTION_IF IS_AN_INT ~%kit_desc%~ BEGIN
		ACTION_GET_STRREF %kit_desc% kit_description 
		OUTER_PATCH_SAVE kit_description ~%kit_description%~ BEGIN
			REPLACE_TEXTUALLY ~%disadvantages%~ ~%restrictions%~
			REPLACE_TEXTUALLY ~%advantages%~ ~%kit_sphere_list%~
		END
		STRING_SET_EVALUATE %kit_desc% ~%kit_description%~
	END
	OUTER_TEXT_SPRINT focus_sphere_list ~~
	OUTER_TEXT_SPRINT major_sphere_list ~~
	OUTER_TEXT_SPRINT minor_sphere_list ~~
	OUTER_TEXT_SPRINT total_sphere_list ~~
	ACTION_IF (%kit_class% = 8) OR  
  			(%kit_class% = 14) OR  
  			(%kit_class% = 15) OR  
  			(%kit_class% = 18) BEGIN
	  ACTION_IF IS_AN_INT ~%kit_code%~ BEGIN
		COPY_EXISTING ~%kit_clab%.2da~ ~override~ //copy the clab of the base kit 
		  COUNT_2DA_COLS cols // amount of columns
		  READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
		  FOR (row = 3; row < rows; ++row) BEGIN // iterate over rows
			READ_2DA_ENTRY_FORMER rows row 0 sphere // read first column
			PATCH_IF (~%sphere%~ STRING_EQUAL_CASE ~SPHERES~) BEGIN	
			  FOR (col = 1; col < cols; ++col) BEGIN // iterate over columns
				READ_2DA_ENTRY_FORMER rows row col ability // read column value
				PATCH_IF (NOT ~%ability%~ STRING_EQUAL ~****~) BEGIN 
				  SET string_length = (STRING_LENGTH ~%ability%~) - 3
				  LPF SUBSTRING INT_VAR start = 3 length = EVAL %string_length% STR_VAR string = EVAL ~%ability%~ RET abil_new = substring END
				  INNER_ACTION BEGIN 
					COPY ~%MOD_FOLDER%/lib/qd_mc/QD_MC_AP.eff~ ~override/%abil_new%#.eff~
					  WRITE_EVALUATED_ASCII 0x30 ~%abil_new%~
					  SET col_length = STRING_LENGTH ~%col%~
					ACTION_IF (%col_length% = 1) BEGIN 
					  COPY_EXISTING ~override/QD_MCP0%col%.spl~ ~override~ 
						LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 9 resist_dispel = 0 power = 0 parameter1 = %kit_code% parameter2 = 9 STR_VAR resource = EVAL ~%abil_new%#~ END  
					END 
					ACTION_IF (%col_length% = 2) BEGIN
					  COPY_EXISTING ~override/QD_MCP%col%.spl~ ~override~ 
						LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 timing = 9 resist_dispel = 0 power = 0 parameter1 = %kit_code% parameter2 = 9 STR_VAR resource = EVAL ~%abil_new%#~ END  
					END 
				  END 
				END
			  END 
			END 
		  END
		BUT_ONLY 
	  END
	END
  END
//___________________________________________________________________________________
